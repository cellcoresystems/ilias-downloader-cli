group 'com.github.thetric.iliasdownloader'

repositories {
    jcenter()
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'net.saliman:gradle-cobertura-plugin:2.4.0'
    }
}

configure(subprojects.findAll { it.childProjects.isEmpty() }) {
    apply plugin: 'groovy'
    apply plugin: 'maven'
    apply plugin: 'codenarc'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    version '2.0.2'
    jar.baseName = 'ilias-downloader'
    jar.appendix = it.name

    dependencies {
        compile 'org.codehaus.groovy:groovy:2.4.11'

        // logging libs
        compile 'org.apache.logging.log4j:log4j-core:2.8.2'

        // test
        testCompile ('org.spockframework:spock-core:1.1-groovy-2.4') {
            exclude group: 'org.codehaus.groovy', module: 'groovy-all'
        }
        testCompile 'net.bytebuddy:byte-buddy:1.7.0'
        testRuntime 'org.slf4j:slf4j-api:1.7.25'

        // code analysis
        codenarc 'org.codenarc:CodeNarc:0.27.0'
    }

    apply from: rootProject.file('config/gradle/coverage.gradle')
    apply from: rootProject.file('config/codenarc.gradle')

    repositories {
        jcenter()
        maven { url "https://jitpack.io" }
    }

    tasks.withType(GroovyCompile) {
        groovyOptions.optimizationOptions.indy = true
    }
}
